plugins {
	id 'base'
	id 'org.springframework.boot' apply false
	id 'io.spring.dependency-management' apply false
	id 'com.github.spotbugs' apply false
    id 'net.ltgt.errorprone' apply false
	id 'net.ltgt.nullaway' apply false
}

subprojects {
	apply plugin: 'java'
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(25)
		}
		withSourcesJar()
		withJavadocJar() //JavaDocJarが不要な場合はコメントにしてください
	}
	test {
		useJUnitPlatform()

		// テストエラーをビルドエラーとしない
		ignoreFailures = ignoreCheckFailures
		finalizedBy 'jacocoTestReport'

		reports {
			// ローカルはeclipse or html、CIはcodebuild/jenkinsで確認
			html.required = true
			junitXml {
				outputPerTestCase = true
				//mergeReruns = true
			}
		}
	}
	sourceSets {
		main {
			java {
				exclude '**/.gitkeep'
			}
			resources {
				exclude '**/.gitkeep'
			}
		}
		test {
			java {
				exclude '**/.gitkeep'
			}
			resources {
				exclude '**/.gitkeep'
			}
		}
		
	}
    apply plugin: 'net.ltgt.errorprone'
	apply plugin: 'net.ltgt.nullaway'
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		options.compilerArgs += ['-Xlint:deprecation,unchecked']
        options.errorprone.nullaway {
			warn()
			jspecifyMode = true
		}
	}
	tasks.named('compileTestJava').configure {
        options.errorprone.enabled = false
    }
	nullaway {
	 	annotatedPackages.add 'com.example.demo' // 各自のパッケージに置き換えてください
	}
	tasks.withType(Javadoc) {
		failOnError = !ignoreCheckFailures
		options.locale = 'ja_JP'
		options.encoding = 'UTF-8'

		// javadoc側ではコメントの存在チェックを行わないようにする。
		// （存在した場合の表記チェックはそのまま残す）
		// 切っ掛けはJDK21のjavadocでデフォルトコンストラクタの存在が強要されるようになった事。
		// このチェックはデフォルトコンストラクタが定義されているがコメントは無いという状態を警告するのではなく、
		// デフォルトコンストラクタの定義が無くても警告が発せられる。
		// つまり、この警告を抑えるにはデフォルトコンストラクタを作成し、コメントも記述しなければならない。
		// しかしそれではlombokを積極的に使っているアプリ側は非常に困る。
		// そこで、javadocではコメントの存在チェックを行わず、checkstyle側でコメントの存在チェックを行うことにした。
		// checkstyleはソースコードとして定義されているものにしかチェックを行わない為、lombokが生成するコードはチェック対象外となる。
		options.addBooleanOption('Xdoclint:all,-missing', true)
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	apply plugin: 'io.spring.dependency-management'
	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
			mavenBom "org.springdoc:springdoc-openapi-bom:3.0.0"
		}
		dependencies {
			dependency 'org.apache.commons:commons-lang3:3.20.0'
			dependency 'com.google.errorprone:error_prone_core:2.45.0'
			dependency 'com.uber.nullaway:nullaway:0.12.15'
		}
	}

	apply plugin: 'jacoco'
	jacoco {
		toolVersion = '0.8.13'
	}
	jacocoTestReport {
		dependsOn 'test'
		reports {
			// ローカルはeclipse or html、CIはcodebuild/jenkinsで確認
			html.required = true
			xml.required = true
		}
	}

	apply plugin: 'checkstyle'
	checkstyle {
		toolVersion = '10.26.1'
		ignoreFailures = ignoreCheckFailures
		configFile = new File("${confDir}/checkstyle/checkstyle.xml")

		// テストに対しては実行しない
		checkstyleTest.enabled = false
	}

	apply plugin: 'com.github.spotbugs'
	spotbugs {
		ignoreFailures = ignoreCheckFailures
		excludeFilter = file("${confDir}/spotbugs/exclusion_filter.xml")
		showProgress = false

		// テストに対しては実行しない
		spotbugsTest.enabled = false
	}
	spotbugsMain {
		// ローカルはeclipse or html、CIはcodebuild/jenkinsで確認
		reports {
			xml {
				required = true
			}
			html {
				required = true
				stylesheet = 'fancy-hist.xsl'
			}
		}
	}

	apply plugin: 'maven-publish'
	publishing {
		publications {
			// Spring Bootにおいて、publishする際の調整
			mavenJava(MavenPublication) {
				from components.java
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}
		}
		repositories {
			// ビルドしたライブラリのjarは、mavenRepositoryの形式で以下のパスに配置される
			maven {
				name = 'local-repo'
				url = rootProject.layout.buildDirectory.dir("local-repo")
			}
		}
	}
}